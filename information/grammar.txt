PROG          -> public class id {  public static void main ( String [ ] id ) {  <CMDS> } } 
CMDS          -> <CMD><MAIS_CMDS> | <CMD_COND><CMDS> | <DC> | λ

DC            -> <VAR> <MAIS_CMDS>
VAR           -> <TIPO> <VARS>
TIPO          -> double
VARS          -> id<MAIS_VAR>
MAIS_VAR      -> ,<VARS> | λ
MAIS_CMDS     -> ;<CMDS>

CMD           -> System.out.println (<EXPRESSAO>)
                 | id <RESTO_IDENT>
RESTO_IDENT   -> = <EXP_IDENT> | (<LISTA_ARG>)
EXP_IDENT     -> <EXPRESSAO> | lerDouble()
LISTA_ARG     -> <ARGUMENTOS> | λ
ARGUMENTOS    -> id <MAIS_IDENT>
MAIS_IDENT    -> , <ARGUMENTOS> | λ

CMD_COND      -> if (  <CONDICAO> )  {<CMDS>} <PFALSA>
                 | while (  <CONDICAO> )  {<CMDS>}
CONDICAO      -> <EXPRESSAO> <RELACAO> <EXPRESSAO>
RELACAO       -> == | != | >= | <= | > | <
PFALSA        -> else { <CMDS> } | λ

EXPRESSAO     -> <TERMO> <OUTROS_TERMOS>
TERMO         -> <OP_UN> <FATOR> <MAIS_FATORES>
OP_UN         -> - | λ
FATOR         -> id | numero_real | (<EXPRESSAO>)
OUTROS_TERMOS -> <OP_AD> <TERMO> <OUTROS_TERMOS> | λ
OP_AD         -> + | -
MAIS_FATORES  -> <OP_MUL> <FATOR> <MAIS_FATORES> | λ
OP_MUL        -> * | /
